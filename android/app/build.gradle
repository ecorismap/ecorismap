apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"
apply plugin: "com.google.android.libraries.mapsplatform.secrets-gradle-plugin"
apply plugin: "com.google.gms.google-services"

// react-native-background-geolocation
def background_geolocation = project(':react-native-background-geolocation')
apply from: "${background_geolocation.projectDir}/app.gradle"


/**
 * This is the configuration block to customize your React Native Android app.
 * By default you don't need to apply any configuration, just uncomment the lines you need.
 */
react {
    /* Autolinking */
    autolinkLibrariesWithApp()
}

// def projectRoot = rootDir.getAbsoluteFile().getParentFile().getAbsolutePath()

// def reactNativeRoot = new File(["node", "--print", "require.resolve('react-native/package.json')"].execute(null, rootDir).text.trim()).getParentFile().getAbsolutePath()

// project.ext.react = [
//     entryFile: ["node", "-e", "require('expo/scripts/resolveAppEntry')", projectRoot, "android"].execute(null, rootDir).text.trim(),
//     enableHermes: true,
//     hermesCommand: new File(["node", "--print", "require.resolve('react-native/package.json')"].execute(null, rootDir).text.trim()).getParentFile().getAbsolutePath() + "/sdks/hermesc/%OS-BIN%/hermesc",
//     cliPath: "${reactNativeRoot}/cli.js",
//     composeSourceMapsPath: "${reactNativeRoot}/scripts/compose-source-maps.js",
// ]

// apply from: new File(reactNativeRoot, "react.gradle")

/**
 * Run Proguard to shrink the Java bytecode in release builds.
 */
def enableProguardInReleaseBuilds = false


def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

// Load local properties
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

android {
    ndkVersion rootProject.ext.ndkVersion

    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion
    namespace "jp.co.ecoris.ecorismap"
    defaultConfig {
        applicationId "jp.co.ecoris.ecorismap"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode Integer.parseInt(System.getenv("VERSION_CODE") ?: "2120")
        versionName '0.5.1'

        // Google Play用：ARM64とARM32のみに限定
        // react-native-virgil-cryptoのx86_64ライブラリが16KB page size非対応のため
        // x86/x86_64を除外（実機はほぼ全てARM）
        ndk {
            abiFilters "arm64-v8a", "armeabi-v7a"
        }
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
        // 16KB page size非対応のx86系ライブラリを明示的に除外
        exclude 'lib/x86_64/**'
        exclude 'lib/x86/**'
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
        release {
            storeFile file(System.getenv("STORE_FILE")?: localProperties.getProperty('MYAPP_UPLOAD_STORE_FILE'))
            storePassword System.getenv("KEYSTORE_PASSWORD") ?: localProperties.getProperty('MYAPP_UPLOAD_STORE_PASSWORD')
            keyAlias System.getenv("KEY_ALIAS") ?: localProperties.getProperty('MYAPP_UPLOAD_KEY_ALIAS')
            keyPassword System.getenv("KEY_PASSWORD") ?: localProperties.getProperty('MYAPP_UPLOAD_KEY_PASSWORD')
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            // Caution! In production, you need to generate your own keystore file.
            // see https://reactnative.dev/docs/signed-apk-android.
            signingConfig signingConfigs.release
            minifyEnabled enableProguardInReleaseBuilds
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
}


dependencies {

    implementation("com.facebook.react:react-android")
    implementation platform('com.google.firebase:firebase-bom:34.3.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-appcheck-playintegrity'
    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }
}
